# A 'sparse array' wrapper

# distutils: language = c++

import numpy as np
cimport numpy as cnp
from numpy cimport (PyArray_SimpleNew, PyArray_DATA, PyArray_SIZE,
                    npy_intp, npy_bool, npy_byte, npy_ubyte,
                    npy_short, npy_int, npy_long, npy_longlong,
                    npy_ushort, npy_uint, npy_ulong, npy_ulonglong,
                    npy_cdouble)
from cpython.object cimport PyObject_IsTrue, PyObject_RichCompare
from cpython.complex cimport (PyComplex_Check, PyComplex_FromDoubles,
                              PyComplex_RealAsDouble, PyComplex_ImagAsDouble)

# Indexing type declarations
from sparr.common_types cimport single_index_type

cdef extern from "complex_ops.h":
    cdef cppclass npy_cdouble_wrapper:
        double real
        double imag

cdef extern from "fixed_cap.h" namespace "sparray":
    cdef cppclass index_type:
        index_type()
        single_index_type& operator[](size_t j) except +

    # this creates index_type instances with fixed size
    cdef cppclass index_factory_t:
        index_factory_t(const int n)
        int ndim() const
        index_type get_new() except +

cdef extern from "fixed_cap.h":
    single_index_type[] FC_ELEMS(index_type) nogil

cdef extern from "sp_map.h" namespace "sparray":
    cdef cppclass map_array_t[T]:
        map_array_t() except +
        map_array_t(const int ndim) except +
        map_array_t(const map_array_t&) except +
        void copy_from[S](const map_array_t[S]*) except +

        size_t ndim() const
        index_type shape() const
        index_type get_min_shape() const
        void set_shape(const index_type&) except +

        T fill_value() const
        void set_fill_value(T value)

        size_t count_nonzero() const

        # single element accessors
        T get_one(const index_type& idx) const 
        void set_one(const index_type& idx, const T& value)


    cdef cppclass operations[T]:
        void todense(const map_array_t[T] *src,
                     void* dest,
                     const single_index_type num_elem) except +
        void to_coo(const map_array_t[T] *src,
                    void *data, void *row, void *clmn,
                    const single_index_type num_elem) except +

        # $x <- alpha f(x) + beta x$
        # default is alpha=1 and beta=0
        void inplace_unary(map_array_t[T] *self,
                           T (*op)(T x))
        void inplace_unary(map_array_t[T] *self,
                           T (*op)(T x),
                           const T alpha, const T beta)

        # $x <- alpha f(x, y) + beta x$, \foreach x in self, \foreach y in other
        void inplace_binop(T (*binop)(T x, T y),
                           map_array_t[T] *self,
                           const map_array_t[T] *other) except +
        void inplace_binop(T (*binop)(T x, T y),
                           map_array_t[T] *self,
                           const map_array_t[T] *other,
                           const T alpha, const T beta) except +

        # $z <- binop(x, y)$ \foreach x in arg1, \foreach y in arg2
        void apply_binop[S](T (*binop)(S x, S y),
                         map_array_t[T] *self,
                         const map_array_t[S] *arg1,
                         const map_array_t[S] *arg2) except +

        # C <- A @ B
        void apply_mmul(map_array_t[T] *C,
                        const map_array_t[T] *A,
                        const map_array_t[T] *B) except +


# Elementary elementwise operations
cdef extern from "elementwise_ops.h" namespace "sparray":
    T add[T](T, T)
    T mul[T](T, T)
    T sub[T](T, T)

    npy_bool equal[S](S, S)
    npy_bool less_equal[S](S, S)
    npy_bool greater_equal[S](S, S)
    npy_bool not_equal[S](S, S)
    npy_bool less[S](S, S)
    npy_bool greater[S](S, S)


cdef extern from "util.h" namespace "sparray":
    int _is_tuple_of_integers(object obj, index_type& idx, int)
    object validate_index[T](index_type& idx,
                             const map_array_t[T] *m,
                             int is_shape_fixed)


{{py:
CTYPES_GEN = ['npy_byte', 'npy_short', 'npy_int', 'npy_long', 'npy_longlong',
              'npy_ubyte', 'npy_ushort', 'npy_uint', 'npy_ulong', 'npy_ulonglong',
              'float', 'double']
TNUMS_GEN = ['cnp.NPY_BYTE', 'cnp.NPY_SHORT', 'cnp.NPY_INT', 'cnp.NPY_LONG', 'cnp.NPY_LONGLONG',
             'cnp.NPY_UBYTE', 'cnp.NPY_USHORT', 'cnp.NPY_UINT', 'cnp.NPY_ULONG', 'cnp.NPY_ULONGLONG',
             'cnp.NPY_FLOAT', 'cnp.NPY_DOUBLE']
CONDS_GEN = ['if'] + ['elif']*(len(TNUMS_GEN) - 1)

# add types which need special casing (eg get/set)
CTYPES = CTYPES_GEN + ['npy_bool', 'npy_cdouble_wrapper']
TNUMS = TNUMS_GEN + ['cnp.NPY_BOOL', 'cnp.NPY_CDOUBLE']
CONDS = CONDS_GEN + ['elif', 'elif']
}}


# instantiate the 'operations' structs for the allowed types
{{for CT in CTYPES}}
cdef operations[{{CT}}] op_{{CT}}
{{endfor}}


# the main polymorphism kludge :-)
cdef union union_t:
{{for CT in CTYPES}}
    map_array_t[{{CT}}] *{{CT}}_ptr
{{endfor}}


cdef class MapArray:
    """A DOK format sparse array.

    A two-dimensional sparse array, suitable for incremental construction.
    Inserting new elements is efficient.
    Semantics of this object should mostly mimic the numpy array.

    Parameters
    ----------
    shape: 2-element tuple of ints, optional
        The shape of the array. Default is zero.
    fill_value : scalar, optional
        The value assumed for the missing entries. Default is zero.
    dtype : optional
        Default is python's float. 

    Attributes
    ----------
    ndim
    shape
    dtype
    fill_value
    
    Methods
    -------
    astype
    copy
    count_nonzero
    todense
    from_dense
    to_coo
    from_coo

    Examples
    --------
    >>> from sp_map import MapArray as M
    >>> m = M()
    >>> m.shape
    (0, 0)

    Inserting new elements expands the array

    >>> m[1, 2] = -101
    >>> m.shape
    (2, 3)
    >>> m[0, 3] = 42
    >>> m.shape
    (2, 4)
    >>> m.todense()
    array([[   0.,    0.,    0.,   42.],
           [   0.,    0., -101.,    0.]])

    Arithmetic operations are elementwise

    >>> m2 = m * m + 1
    >>> m2.to_coo()
    (array([  1765.,  10202.]), (array([0, 1]), array([3, 2])))

    """
    cdef union_t p
    cdef int typenum
    cdef int _is_shape_fixed
    cdef index_factory_t *index_factory

    __array_priority__ = 10.1     # equal to that of sparse matrices

    def __init__(self, shape=None, ndim=None, fill_value=None, dtype=None):
        pass

    def __cinit__(self, shape=None, ndim=None, fill_value=None, dtype=None, *args, **kwds):

        if dtype is None:
            if fill_value is None:
                dtype = np.dtype(float)
                fill_value = 0.0
            else:
                dtype = np.asarray(fill_value).dtype
        else:
            if fill_value is None:
                fill_value = 0

        # this seems to convert float etc to numpy dtypes
        dtype = np.dtype(dtype)
        self.typenum = dtype.num

        # shape/ndim: if none are given, default is ndim=2, shape=None
        if shape is None:
            self._is_shape_fixed = False
            if ndim is None:
                ndim = 2
            shape = (0,)*ndim
        else:
            self._is_shape_fixed = True
            if ndim is None:
                ndim = len(shape)
            else:
                if len(shape) != ndim:
                    raise ValueError("shape = %s and ndim= %s are ambiguous" %
                                     (shape, ndim))

        # have ndim and shape, validate
        if ndim != int(ndim):
            raise ValueError("ndim: Expected integer, got %s." % ndim)
        cdef int nd = ndim
        self.index_factory = new index_factory_t(nd)

        cdef index_type shp = self.index_factory.get_new()
        if not _is_tuple_of_integers(shape, shp, nd):
                raise TypeError("Shape %s not undestood." % str(shape))

        # allocate memory on the correct type pointer
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            self.p.{{CT}}_ptr = new map_array_t[{{CT}}](nd)
        {{endfor}}
        else:
            raise TypeError("dtype %s  not supported." % dtype)

        # Finally, propagate the shape to lower level
        {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            self.p.{{C}}_ptr.set_shape(shp)
        {{endfor}}
        else:
            raise TypeError("shape setter: dtype = %s unhandled." % self.dtype)

        #handle fill_value
        self.fill_value = fill_value

    def __dealloc__(self):
        del self.index_factory
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            del self.p.{{CT}}_ptr
        {{endfor}}
        # NB: this if-then-else is deliberately not closed off with an error.
        # The reason is that if a dtype lookup fails in __cinit__, it raises
        # a TypeError, and then __dealloc__ is called by Cython anyway.
        # The result is a spurious message from __dealloc__ saying that
        # the error was ignored.

    #### Public interface accessors #####

    property dtype:
        def __get__(self):
            # per https://mail.scipy.org/pipermail/numpy-discussion/2013-October/068002.html
            return np.dtype(cnp.PyArray_TypeObjectFromType(self.typenum))

    property ndim:
        def __get__(self):
            cdef int nd
            {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
            {{IF}} self.typenum == {{NUM}}:
                nd = self.p.{{C}}_ptr.ndim()
                if nd != self.index_factory.ndim():
                    raise RuntimeError("Panic: self._ndim = %s != ptr.ndim = %s."
                                        % (self.index_factory.ndim(), nd))
                return nd
            {{endfor}}
            else:
                raise TypeError("ndim: dtype = %s unhandled." % self.dtype)

    property fill_value:
        def __get__(self):
            cdef npy_cdouble_wrapper z
            {{for NUM, C, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
            {{IF}} self.typenum == {{NUM}}:
                return self.p.{{C}}_ptr.fill_value()
            {{endfor}}
            elif self.typenum == cnp.NPY_BOOL:
                return self.p.npy_bool_ptr.fill_value() != 0
            elif self.typenum == cnp.NPY_CDOUBLE:
                z = self.p.npy_cdouble_wrapper_ptr.fill_value()
                return PyComplex_FromDoubles(z.real, z.imag)
            else:
                raise TypeError("fill_value: dtype = %s unhandled." % self.dtype)

        def __set__(self, value):
            cdef int ival
            cdef npy_cdouble_wrapper z
            {{for NUM, C, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
            {{IF}} self.typenum == {{NUM}}:
                self.p.{{C}}_ptr.set_fill_value(value)
            {{endfor}}
            elif self.typenum == cnp.NPY_BOOL:
                ival = PyObject_IsTrue(value)
                if ival == -1:
                    raise ValueError("Cannot interpet %s as a boolean" % value)
                self.p.npy_bool_ptr.set_fill_value(ival)
            elif self.typenum == cnp.NPY_CDOUBLE:
                ival = PyComplex_Check(value)
                if ival == -1:
                    raise ValueError("Cannot interpret %s as a complex number" % value)
                z.real = PyComplex_RealAsDouble(value)
                z.imag = PyComplex_ImagAsDouble(value)
                self.p.npy_cdouble_wrapper_ptr.set_fill_value(z)
            else:
                raise TypeError("fill_value: dtype = %s unhandled." % self.dtype)

    property shape:
        def __get__(self):
            cdef index_type sh = self.index_factory.get_new()
            cdef int nd = self.index_factory.ndim()
            {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
            {{IF}} self.typenum == {{NUM}}:
                sh = self.p.{{C}}_ptr.shape()
                return tuple(sh[i] for i in range(nd))
            {{endfor}}
            else:
                raise TypeError("shape: dtype = %s unhandled." % self.dtype)

    property is_shape_fixed:
        def __get__(self):
            return bool(self._is_shape_fixed)

        def __set__(self, value):
            self._is_shape_fixed = 1 if value else 0

    def count_nonzero(self):
        {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            return self.p.{{C}}_ptr.count_nonzero()
        {{endfor}}
        raise TypeError("count_nonzero: dtype = %s unhandled." % self.dtype)


    def copy(self, dtype=None):
        """Return a copy of self, optionally casting it to a specified dtype.

        Parameters
        ----------
        dtype : optional
            dtype to cast the copy to. Default is to preserve the dtype.

        See Also
        -----
        astype

        """
        if dtype is None:
            dtype = self.dtype
        newobj = MapArray(dtype=dtype, shape=self.shape)
        newobj.is_shape_fixed = False

        cdef int typenum_src = self.typenum
        cdef int typenum_dest = newobj.typenum
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} typenum_src == {{NUM}}:
            # found self.dtype, on to the dest dtype now
            {{for NUM1, CT1, IF1 in zip(TNUMS, CTYPES, CONDS)}}
            {{IF1}} typenum_dest == {{NUM1}}:
                newobj.p.{{CT1}}_ptr.copy_from[{{CT}}](self.p.{{CT}}_ptr)
                return newobj
            {{endfor}}
        {{endfor}}
        raise TypeError("copy: never be here. dtype = " % self.dtype)

    def astype(self, dtype):
        """Return a copy of self, cast to a specified dtype.

        Parameters
        ----------
        dtype : dtype to cast the copy to.

        See Also
        -----
        copy

        """
        # Syntactic sugar: .copy() preserves dtype, .astype changes it.
        return self.copy(dtype)

    def __repr__(self):
        return ("<%dx%d sparse array of type '%s' with %d stored elements.>" %
               (self.shape + (self.dtype.type, self.count_nonzero())))

    def todense(self):
        """Return the dense array representation of self.

        Construct a 2D numpy array of matching shape, dtype and data.

        """
        cdef int nd
        cdef cnp.ndarray a

        {{for NUM, CT in zip(TNUMS, CTYPES)}}
        if self.typenum == {{NUM}}:
            nd = <int>self.p.{{CT}}_ptr.ndim()
            a = PyArray_SimpleNew(nd,
                                  <npy_intp*>FC_ELEMS(self.p.{{CT}}_ptr.shape()),
                                  {{NUM}})
            op_{{CT}}.todense(self.p.{{CT}}_ptr, PyArray_DATA(a), PyArray_SIZE(a))
            return a
        {{endfor}}
        raise TypeError("todense: never be here. dtype = " % self.dtype)

    @classmethod
    def from_dense(cls, arr):
        """Construct from a dense array.
        """
        self = MapArray(dtype=arr.dtype, ndim=arr.ndim)
        for idx, val in np.ndenumerate(arr):
            self[idx] = val
        return self

    def to_coo(self):
        """Return COO style arrays for data, rows and columns.

        Returns
        -------
        data : ndarray
            Non-zero values.
        (row, col) : tuple of ndarrays
            Indices of nonzero elements, such that 
            ``self[row[k], col[k]] = data[k]``.

        Notes
        -----
        This function ignores the `fill_value`.

        """
        if self.ndim != 2:
            raise NotImplementedError("to_coo is only implemented for 2D arrays.")

        cdef npy_intp[1] sh
        cdef cnp.ndarray row, clmn, data

        {{for NUM, CT in zip(TNUMS, CTYPES)}}
        if self.typenum == {{NUM}}:
            sh[0] = <npy_intp>self.p.{{CT}}_ptr.count_nonzero()
            row = PyArray_SimpleNew(1, sh, cnp.NPY_INTP)   # XXX: int32 indices?
            clmn = PyArray_SimpleNew(1, sh, cnp.NPY_INTP)
            data = PyArray_SimpleNew(1, sh, {{NUM}})
            op_{{CT}}.to_coo(self.p.{{CT}}_ptr,
                             PyArray_DATA(data),
                             PyArray_DATA(row),
                             PyArray_DATA(clmn),
                             PyArray_SIZE(data))
            return data, (row, clmn)
        {{endfor}}

    @classmethod
    def from_coo(cls, data, row_col):
        """Construct from a COO style triplet of arrays.

        Given three 1D arrays, `data`, `row` and `col`, return a MapArray
        instance ``m`` such that ``m[row[k], col[k]] = data[k]`` for
        ``k in range(n)``.

        Parameters
        ----------
        data : array_like, shape (n, )
            Nonzero values.
        (row, col) :  tuple of array_likes, both shape (n,)
            Indices of nonzero elements.

        """
        row, col = row_col
        self = MapArray(dtype=data.dtype, ndim=2)
        for val, r, c in zip(data, row, col):
            self[r, c] = val
        return self

    ##### Single element access #####

    def __getitem__(self, tpl):
        # no slicing, single-element access only
        cdef index_type idx = self.index_factory.get_new()
        if not _is_tuple_of_integers(tpl, idx, self.index_factory.ndim()):
            raise IndexError("%s cannot be interpreted as an index into "
                             " an array." % str(tpl))

        {{for NUM, CT in zip(TNUMS_GEN, CTYPES_GEN)}}
        if self.typenum == {{NUM}}:
            validate_index[{{CT}}](idx, self.p.{{CT}}_ptr, True)
            return self.p.{{CT}}_ptr.get_one(idx)

        {{endfor}}
        if self.typenum == cnp.NPY_BOOL:
            validate_index[npy_bool](idx, self.p.npy_bool_ptr, True)
            return self.p.npy_bool_ptr.get_one(idx) != 0

        cdef npy_cdouble_wrapper z
        if self.typenum == cnp.NPY_CDOUBLE:
            validate_index[npy_cdouble_wrapper](idx, self.p.npy_cdouble_wrapper_ptr, True)
            z = self.p.npy_cdouble_wrapper_ptr.get_one(idx)
            return PyComplex_FromDoubles(z.real, z.imag)
        raise TypeError("__getitem__: dtype = %s not understood." % self.dtype)

    def __setitem__(self, tpl, value):
        cdef index_type idx = self.index_factory.get_new()
        if not _is_tuple_of_integers(tpl, idx, self.index_factory.ndim()):
            raise IndexError("%s cannot be interpreted as an index into "
                             " an array." % str(tpl))
        cdef int ival
        cdef npy_cdouble_wrapper z

        {{for NUM, CT, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
        {{IF}} self.typenum == {{NUM}}:
            validate_index[{{CT}}](idx, self.p.{{CT}}_ptr, self._is_shape_fixed)
            self.p.{{CT}}_ptr.set_one(idx, value)

        {{endfor}}
        elif self.typenum == cnp.NPY_BOOL:
            validate_index[npy_bool](idx, self.p.npy_bool_ptr, self._is_shape_fixed)
            ival = PyObject_IsTrue(value)
            if ival == -1:
                raise ValueError("Cannot interpet %s as a boolean" % value)
            self.p.npy_bool_ptr.set_one(idx, ival)

        elif self.typenum == cnp.NPY_CDOUBLE:
            validate_index[npy_cdouble_wrapper](idx,
                                                self.p.npy_cdouble_wrapper_ptr,
                                                self._is_shape_fixed)
            ival = PyComplex_Check(value)
            if ival == -1:
                raise ValueError("Cannot interpret %s as a complex number" % value)
            z.real = PyComplex_RealAsDouble(value)
            z.imag = PyComplex_ImagAsDouble(value)
            self.p.npy_cdouble_wrapper_ptr.set_one(idx, z)
        else:
            raise TypeError("__setitem__: dtype = %s not understood." % self.dtype)

    ###### Arithmetics #######

    # XXX maybe add more arithm ops: div, l/r shifts, mod etc

    {{for op, symb in zip(["add", "mul", "sub"], ["+", "*", "-"])}}
    def __i{{op}}__(MapArray self not None, other):
        if isinstance(other, np.ndarray): 
            # hand over to __{{op}}__ for densification
            return NotImplemented

        if isinstance(other, MapArray):
            return _i{{op}}_impl(self, other)

        # try checking if other quacks like a scipy.sparse matrix
        if hasattr(other, 'tocoo'):
            coo_other = _try_converting_from_sparse(other)
            if coo_other is NotImplemented:
                return coo_other
            return _i{{op}}_impl(self, coo_other)

        # else, other must be a scalar
        try:
            dt = np.asarray(other).dtype     # XXX there must be a more direct way
            arr_other = MapArray(shape=self.shape,
                                 dtype=dt,
                                 fill_value=other)
            return _i{{op}}_impl(self, arr_other)
        except TypeError:
            # failed to convert other to MapArray. Give up.
            return NotImplemented

    def __{{op}}__(self, other):
        if isinstance(self, MapArray):
            if isinstance(other, np.ndarray):
                # Densify and return dense result
                return self.todense() {{symb}} other
            else:
                newobj = self.copy()
                return newobj.__i{{op}}__(other)
        else:
            # other must be a MapArray
            if isinstance(self, np.ndarray):
                return self {{symb}} other.todense()

            # try checking if self quacks like a scipy.sparse matrix
            if hasattr(self, 'tocoo'):
                # csr*map is ambiguous: is it matrix multiply or elementwise?
                # refuse the temptation to guess and give up.
                # FIXME: this actually fails to work. The reason is that
                # sparse matrices check dimensions *before* handing over to
                # MapArray. Maybe the right thing to do is to bail out
                # from both csr * map and map * csr (even though the latter works).
                {{if op == "mul"}}
                raise ValueError("matrix times array is ambiguous. Please "
                                 "decide if you want matrix or elementwise "
                                 "multiplication.")
                {{else}}
                coo_self = _try_converting_from_sparse(self)
                if coo_self is NotImplemented:
                    return coo_self
                return _i{{op}}_impl(coo_self, other)
                {{endif}}

            # else, self must be a scalar
            try:
                dt = np.asarray(self).dtype     # XXX there must be a more direct way
                arr_self = MapArray(shape=other.shape,
                                     dtype=dt,
                                     fill_value=self)
                return _i{{op}}_impl(arr_self, other)
            except TypeError:
                # conversion to MapArray failed. Give up.
                return NotImplemented

    {{endfor}}

    ########### Booleans ########################

    def __richcmp__(self, other, int op):
        if not isinstance(self, MapArray):
            # __richcmp__ seems to sort out reversed ops ($1 < m$ -> $m >= 1$)
            # automagically
            raise ValueError("__richcmp__: %s %s %s" % (self, other, op))

        if isinstance(other, np.ndarray):
            return PyObject_RichCompare(self.todense(), other, op)

        if isinstance(other, MapArray):
            return _richcmp_impl(self, other, op)

        if hasattr(other, "tocoo"):
            coo_other = _try_converting_from_sparse(other)
            if coo_other is NotImplemented:
                return coo_other
            return _richcmp_impl(self, coo_other, op)            

        # else, other is a scalar
        dt = np.asarray(other).dtype     # XXX there must be a more direct way
        arr_other = MapArray(shape=self.shape,
                             dtype=dt,
                             fill_value=other)
        return _richcmp_impl(self, arr_other, op)

    def __matmul__(self, A):
        if isinstance(self, np.ndarray):
            return self.__matmul__(A.todense())
        if isinstance(A, np.ndarray):
            return self.todense().__matmul__(A)

        if not isinstance(self, MapArray) or not isinstance(A, MapArray):
            raise NotImplementedError("matmul only handles sparse arrays.")

        if self.ndim != 2 or A.ndim != 2:
            raise NotImplementedError("matmul is only implemented for 2D arrays.")

        if self.fill_value != 0 or A.fill_value != 0:
            raise NotImplementedError("The result is likely dense.")

        # figure out the result dtype and perform multiplication
        dtyp = np.promote_types(self.dtype, A.dtype)
        newobj = MapArray(dtype=dtyp)
        _mmul_impl(newobj, self, A)
        return newobj

    def __imatmul__(self, A):
        raise NotImplementedError("In-place matmul is not implemented. "
                                  "Use a = a @ b instead of a @= b.")

    def __array_prepare__(self, array, context=None):
        print("Array_prepare!", array, context)
        return self.todense()
    

##### implementations

def _mmul_impl(MapArray self not None,
               MapArray A not None,
               MapArray B not None):

    # handle types
    if A.dtype != self.dtype:
        AA = A.astype(self.dtype)
        return _mmul_impl(self, AA, B)
    if B.dtype != self.dtype:
        BB = B.astype(self.dtype)
        return _mmul_impl(self, A, BB)

    # by now all arrays are of the same dtype
    {{for NUM, CT in zip(TNUMS, CTYPES)}}
    if self.typenum == {{NUM}}:
        op_{{CT}}.apply_mmul(self.p.{{CT}}_ptr,
                             A.p.{{CT}}_ptr,
                             B.p.{{CT}}_ptr)
        return self
    {{endfor}}
    raise NotImplementedError


def _richcmp_impl(MapArray self not None, MapArray other not None, int op):
    """ Sparse <op> sparse implementation of __richcmp__.

        Figure out the common dtype, copy if necessary. Then dispatch
        on the type.

        XXX: this can be made copy less (ditto for binops).
    """
    # figure out dtypes, copy if necessary         
    if self.typenum != other.typenum:
        res_type = np.promote_types(self.dtype, other.dtype)
        if res_type.num != self.typenum:
            # upcast self and operate on the copy
            newobj = self.astype(res_type)
            return _richcmp_impl(newobj, other, op)

        # by this stage, self.dtype == res_type
        if self.typenum != other.typenum:
            # upcast other
            other = other.astype(res_type)

    # by now, both self and other are of the same dtype
    result = MapArray(dtype=bool, ndim=self.ndim)

    # dispatch on the types
    {{for NUM, CT in zip(TNUMS, CTYPES)}}
    if self.typenum == {{NUM}}:
        if op == 0:
            op_npy_bool.apply_binop[{{CT}}](less[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 1:
            op_npy_bool.apply_binop[{{CT}}](less_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 2:
            op_npy_bool.apply_binop[{{CT}}](equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 3:
            op_npy_bool.apply_binop[{{CT}}](not_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 4:
            op_npy_bool.apply_binop[{{CT}}](greater[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 5:
            op_npy_bool.apply_binop[{{CT}}](greater_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        else:
            raise ValueError("sparse cmp: op = ." % op)
        return result

    {{endfor}}


{{for op in ["add", "mul", "sub"]}}
def _i{{op}}_impl(MapArray self not None, MapArray other not None):
    """ Sparse <op> sparse implementation of __i{{op}}__.

        Figure out the common dtype, copy if necessary. Then dispatch
        on the type.
    """
    # figure out dtypes, copy if necessary
    if self.typenum != other.typenum:
        res_type = np.promote_types(self.dtype, other.dtype)
        if res_type.num != self.typenum:
            # upcast self and operate on the copy
            newobj = self.astype(res_type)
            return _i{{op}}_impl(newobj, other)

        # by this stage, self.dtype == res_type
        if self.typenum != other.typenum:
            # upcast other
            other = other.astype(res_type)

    # dispatch
    {{for num, ct in zip(TNUMS, CTYPES)}}
    if self.typenum == {{num}}:
        op_{{ct}}.inplace_binop({{op}}[{{ct}}],
                                self.p.{{ct}}_ptr,
                                other.p.{{ct}}_ptr)
        return self
    {{endfor}}
    raise NotImplementedError("_i{{op}}_: typecode %s not understood." % self.typenum)
{{endfor}}


def _try_converting_from_sparse(what):
    """Convert a scipy.sparse matrix to MapArray. To be used in binops.
    """
    coo = what.tocoo()
    try:
        data, row, col = coo.data, coo.row, coo.col
    except AttributeError:
        return NotImplemented
    row = row.astype(np.intp)  # XXX how MapArray.from_coo wants it
    col = col.astype(np.intp)
    return MapArray.from_coo(data, (row, col))


cnp.import_array()
