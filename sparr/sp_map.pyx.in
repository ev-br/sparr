# A 'sparse array' wrapper

# distutils: language = c++

import numpy as np
cimport numpy as cnp
from numpy cimport PyArray_SimpleNew, PyArray_DATA, PyArray_SIZE, npy_intp, npy_bool
from cpython.object cimport PyObject_IsTrue, PyObject_RichCompare

# Indexing type declarations
from sparr.common_types cimport single_index_type

cdef extern from "util.h" namespace "sparray":
    inline int _is_tuple_of_integers(object obj)

cdef extern from "fixed_cap.h" namespace "sparray":
    cdef cppclass index_type:
        single_index_type& operator[](size_t j) except +

cdef extern from "fixed_cap.h":
    single_index_type[] FC_ELEMS(index_type) nogil


cdef extern from "sp_map.h" namespace "sparray":
    cdef cppclass map_array_t[T]:
        map_array_t() except +
        map_array_t(const map_array_t&) except +
        void copy_from[S](const map_array_t[S]*) except +

        size_t ndim() const
        index_type shape() const
        index_type get_min_shape() const
        void set_shape(const index_type&) except +

        T fill_value() const
        void set_fill_value(T value)

        size_t count_nonzero() const

        # single element accessors
        T get_one(const index_type& idx) const 
        void set_one(const index_type& idx, const T& value)


    cdef cppclass operations[T]:
        void todense(const map_array_t[T] *src,
                     void* dest,
                     const single_index_type num_elem) except +
        void to_coo(const map_array_t[T] *src,
                    void *data, void *row, void *clmn,
                    const single_index_type num_elem) except +

        # $x <- alpha f(x) + beta x$
        # default is alpha=1 and beta=0
        void inplace_unary(map_array_t[T] *self,
                           T (*op)(T x))
        void inplace_unary(map_array_t[T] *self,
                           T (*op)(T x),
                           const T alpha, const T beta)

        # $x <- alpha f(x, y) + beta x$, \foreach x in self, \foreach y in other
        void inplace_binop(T (*binop)(T x, T y),
                           map_array_t[T] *self,
                           const map_array_t[T] *other) except +
        void inplace_binop(T (*binop)(T x, T y),
                           map_array_t[T] *self,
                           const map_array_t[T] *other,
                           const T alpha, const T beta) except +

        # $z <- binop(x, y)$ \foreach x in arg1, \foreach y in arg2
        void apply_binop[S](T (*binop)(S x, S y),
                         map_array_t[T] *self,
                         const map_array_t[S] *arg1,
                         const map_array_t[S] *arg2) except +

        # C <- A @ B
        void apply_mmul(map_array_t[T] *C,
                        const map_array_t[T] *A,
                        const map_array_t[T] *B) except +


# Elementary elementwise operations
cdef extern from "elementwise_ops.h" namespace "sparray":
    T add[T](T, T)
    T mul[T](T, T)
    T sub[T](T, T)

    npy_bool equal[S](S, S)
    npy_bool less_equal[S](S, S)
    npy_bool greater_equal[S](S, S)
    npy_bool not_equal[S](S, S)
    npy_bool less[S](S, S)
    npy_bool greater[S](S, S)


{{py:
CTYPES_GEN = ['long', 'float', 'double']
TNUMS_GEN = ['cnp.NPY_LONG', 'cnp.NPY_FLOAT', 'cnp.NPY_DOUBLE']
CONDS_GEN = ['if'] + ['elif']*(len(TNUMS_GEN) - 1)

# add types which need special casing (eg get/set)
CTYPES = CTYPES_GEN + ['npy_bool']
TNUMS = TNUMS_GEN + ['cnp.NPY_BOOL']
CONDS = CONDS_GEN + ['elif']
}}


# instantiate the 'operations' structs for the allowed types
{{for CT in CTYPES}}
cdef operations[{{CT}}] op_{{CT}}
{{endfor}}


# the main polymorphism kludge :-)
cdef union union_t:
{{for CT in CTYPES}}
    map_array_t[{{CT}}] *{{CT}}_ptr
{{endfor}}


cdef class MapArray:
    """A DOK format sparse array.

    A two-dimensional sparse array, suitable for incremental construction.
    Inserting new elements is efficient.
    Semantics of this object should mostly mimic the numpy array.

    Parameters
    ----------
    shape: 2-element tuple of ints, optional
        The shape of the array. Default is zero.
    fill_value : scalar, optional
        The value assumed for the missing entries. Default is zero.
    dtype : optional
        Default is python's float. 

    Attributes
    ----------
    ndim
    shape
    dtype
    fill_value
    
    Methods
    -------
    astype
    copy
    count_nonzero
    todense
    from_dense
    to_coo
    from_coo

    Examples
    --------
    >>> from sp_map import MapArray as M
    >>> m = M()
    >>> m = M()
    >>> m.shape
    (0, 0)

    Inserting new elements expands the array

    >>> m[1, 2] = -101
    >>> m.shape
    (2, 3)
    >>> m[0, 3] = 42
    >>> m.shape
    (2, 4)
    >>> m.todense()
    array([[   0.,    0.,    0.,   42.],
           [   0.,    0., -101.,    0.]])

    Arithmetic operations are elementwise

    >>> m2 = m * m + 1
    >>> m2.to_coo()
    (array([  1765.,  10202.]), (array([0, 1]), array([3, 2])))

    """
    cdef union_t p
    cdef int typenum

    __array_priority__ = 10.1     # equal to that of sparse matrices

    def __init__(self, shape=None, fill_value=0, dtype=float):
        pass

    def __cinit__(self, shape=None, fill_value=0, dtype=float, *args, **kwds):

        # this seems to convert float etc to numpy dtypes
        dtype = np.dtype(dtype)
        self.typenum = dtype.num

        # allocate memory on the correct type pointer
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            self.p.{{CT}}_ptr = new map_array_t[{{CT}}]()
        {{endfor}}
        else:
            raise TypeError("dtype %s  not supported." % dtype)

        # handle shape
        try:
            self._set_shape(shape)
        except TypeError:
            raise TypeError("Shape must be a tuple, got %s." % shape)

        #handle fill_value
        self.fill_value = fill_value

    def __dealloc__(self):
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            del self.p.{{CT}}_ptr
        {{endfor}}
        # NB: this if-then-else is deliberately not closed off with an error.
        # The reason is that if a dtype lookup fails in __cinit__, it raises
        # a TypeError, and then __dealloc__ is called by Cython anyway.
        # The result is a spurious message from __dealloc__ saying that
        # the error was ignored.

    #### Public interface accessors #####

    property dtype:
        def __get__(self):
            # per https://mail.scipy.org/pipermail/numpy-discussion/2013-October/068002.html
            return np.dtype(cnp.PyArray_TypeObjectFromType(self.typenum))

    property ndim:
        def __get__(self):
            {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
            {{IF}} self.typenum == {{NUM}}:
                return self.p.{{C}}_ptr.ndim()
            {{endfor}}
            else:
                raise TypeError("ndim: dtype = %s unhandled." % self.dtype)

    property fill_value:
        def __get__(self):
            {{for NUM, C, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
            {{IF}} self.typenum == {{NUM}}:
                return self.p.{{C}}_ptr.fill_value()
            {{endfor}}
            elif self.typenum == cnp.NPY_BOOL:
                return self.p.npy_bool_ptr.fill_value() != 0
            else:
                raise TypeError("fill_value: dtype = %s unhandled." % self.dtype)

        def __set__(self, value):
            cdef int ival
            {{for NUM, C, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
            {{IF}} self.typenum == {{NUM}}:
                self.p.{{C}}_ptr.set_fill_value(value)
            {{endfor}}
            elif self.typenum == cnp.NPY_BOOL:
                ival = PyObject_IsTrue(value)
                if ival == -1:
                    raise ValueError("Cannot interpet %s as a boolean" % value)
                self.p.npy_bool_ptr.set_fill_value(ival)
            else:
                raise TypeError("fill_value: dtype = %s unhandled." % self.dtype)

    property shape:
        def __get__(self):
            cdef index_type sh
            {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
            {{IF}} self.typenum == {{NUM}}:
                sh = self.p.{{C}}_ptr.shape()
                return sh[0], sh[1]           # TODO: ndim != 2
            {{endfor}}
            else:
                raise TypeError("shape: dtype = %s unhandled." % self.dtype)

    cdef void _set_shape(self, object shape):
        # shape setter is private, only call it from __cinit__
        cdef int j, nd = self.ndim
        if not shape:
            shape = (0,)*nd

        if not _is_tuple_of_integers(shape):
            raise TypeError("Shape %s not undestood." % str(shape))

        cdef index_type shp
        for j in range(nd):
            shp[j] = shape[j]

        {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            self.p.{{C}}_ptr.set_shape(shp)
        {{endfor}}
        else:
            raise TypeError("shape setter: dtype = %s unhandled." % self.dtype)

    def count_nonzero(self):
        {{for NUM, C, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} self.typenum == {{NUM}}:
            return self.p.{{C}}_ptr.count_nonzero()
        {{endfor}}
        raise TypeError("count_nonzero: dtype = %s unhandled." % self.dtype)


    def copy(self, dtype=None):
        """Return a copy of self, optionally casting it to a specified dtype.

        Parameters
        ----------
        dtype : optional
            dtype to cast the copy to. Default is to preserve the dtype.

        See Also
        -----
        astype

        """
        if dtype is None:
            dtype = self.dtype
        newobj = MapArray(dtype=dtype)

        cdef int typenum_src = self.typenum
        cdef int typenum_dest = newobj.typenum
        {{for NUM, CT, IF in zip(TNUMS, CTYPES, CONDS)}}
        {{IF}} typenum_src == {{NUM}}:
            # found self.dtype, on to the dest dtype now
            {{for NUM1, CT1, IF1 in zip(TNUMS, CTYPES, CONDS)}}
            {{IF1}} typenum_dest == {{NUM1}}:
                newobj.p.{{CT1}}_ptr.copy_from[{{CT}}](self.p.{{CT}}_ptr)
                return newobj
            {{endfor}}
        {{endfor}}
        raise TypeError("copy: never be here. dtype = " % self.dtype)

    def astype(self, dtype):
        """Return a copy of self, cast to a specified dtype.

        Parameters
        ----------
        dtype : dtype to cast the copy to.

        See Also
        -----
        copy

        """
        # Syntactic sugar: .copy() preserves dtype, .astype changes it.
        return self.copy(dtype)

    def todense(self):
        """Return the dense array representation of self.

        Construct a 2D numpy array of matching shape, dtype and data.

        """
        cdef int nd
        cdef cnp.ndarray a

        {{for NUM, CT in zip(TNUMS, CTYPES)}}
        if self.typenum == {{NUM}}:
            nd = <int>self.p.{{CT}}_ptr.ndim()
            a = PyArray_SimpleNew(nd,
                                  <npy_intp*>FC_ELEMS(self.p.{{CT}}_ptr.shape()),
                                  {{NUM}})
            op_{{CT}}.todense(self.p.{{CT}}_ptr, PyArray_DATA(a), PyArray_SIZE(a))
            return a
        {{endfor}}
        raise TypeError("todense: never be here. dtype = " % self.dtype)

    @classmethod
    def from_dense(cls, arr):
        """Construct from a dense array.
        """
        self = MapArray(dtype=arr.dtype)
        for idx, val in np.ndenumerate(arr):
            self[idx] = val
        return self

    def to_coo(self):
        """Return COO style arrays for data, rows and columns.

        Returns
        -------
        data : ndarray
            Non-zero values.
        (row, col) : tuple of ndarrays
            Indices of nonzero elements, such that 
            ``self[row[k], col[k]] = data[k]``.

        Notes
        -----
        This function ignores the `fill_value`.

        """
        cdef npy_intp[1] sh
        cdef cnp.ndarray row, clmn, data

        {{for NUM, CT in zip(TNUMS, CTYPES)}}
        if self.typenum == {{NUM}}:
            sh[0] = <npy_intp>self.p.{{CT}}_ptr.count_nonzero()
            row = PyArray_SimpleNew(1, sh, cnp.NPY_INTP)   # XXX: int32 indices?
            clmn = PyArray_SimpleNew(1, sh, cnp.NPY_INTP)
            data = PyArray_SimpleNew(1, sh, {{NUM}})
            op_{{CT}}.to_coo(self.p.{{CT}}_ptr,
                             PyArray_DATA(data),
                             PyArray_DATA(row),
                             PyArray_DATA(clmn),
                             PyArray_SIZE(data))
            return data, (row, clmn)
        {{endfor}}

    @classmethod
    def from_coo(cls, data, row_col):
        """Construct from a COO style triplet of arrays.

        Given three 1D arrays, `data`, `row` and `col`, return a MapArray
        instance ``m`` such that ``m[row[k], col[k]] = data[k]`` for
        ``k in range(n)``.

        Parameters
        ----------
        data : array_like, shape (n, )
            Nonzero values.
        (row, col) :  tuple of array_likes, both shape (n,)
            Indices of nonzero elements.

        """
        row, col = row_col
        self = MapArray(dtype=data.dtype)
        for val, r, c in zip(data, row, col):
            self[r, c] = val
        return self

    ##### Single element access #####

    def __getitem__(self, tpl):
        # 2D is hardcoded; no slicing
        if not _is_tuple_of_integers(tpl):
            raise IndexError("%s cannot be interpreted as an index into "
                             " 2D array.")

        # validate indices, normalize if needed.
        cdef index_type idx
        cdef single_index_type ii
        {{for i in range(2)}}
        idx[{{i}}] = tpl[{{i}}]
        ii = idx[{{i}}]
        if ii < 0:
            ii += self.shape[{{i}}]
        if ii < 0 or ii >= self.shape[{{i}}]:
            raise IndexError("index %s is out of bounds for axis %s with "
                             "size %s" % (tpl[{{i}}], {{i}}, self.shape[{{i}}]))
        idx[{{i}}] = ii
        {{endfor}}

        {{for NUM, CT in zip(TNUMS_GEN, CTYPES_GEN)}}
        if self.typenum == {{NUM}}:
            return self.p.{{CT}}_ptr.get_one(idx)
        {{endfor}}
        if self.typenum == cnp.NPY_BOOL:
            return self.p.npy_bool_ptr.get_one(idx) != 0
        raise TypeError("__getitem__: dtype = %s not understood." % self.dtype)

    def __setitem__(self, tpl, value):
        if not _is_tuple_of_integers(tpl):
            raise IndexError("%s cannot be interpreted as an index into "
                             " 2D array.")

        # validate indices, normalize if needed.
        cdef index_type idx
        cdef single_index_type ii
        {{for i in range(2)}}
        idx[{{i}}] = tpl[{{i}}]
        ii = idx[{{i}}]
        if ii < 0:
            ii += self.shape[{{i}}]
        if ii < 0:
            raise IndexError("index %s is out of bounds for axis %s with "
                             "size %s" % (tpl[{{i}}], {{i}}, self.shape[{{i}}]))
        idx[{{i}}] = ii
        {{endfor}}

        cdef int ival
        {{for NUM, CT, IF in zip(TNUMS_GEN, CTYPES_GEN, CONDS_GEN)}}
        {{IF}} self.typenum == {{NUM}}:
            self.p.{{CT}}_ptr.set_one(idx, value)
        {{endfor}}
        elif self.typenum == cnp.NPY_BOOL:
            ival = PyObject_IsTrue(value)
            if ival == -1:
                raise ValueError("Cannot interpet %s as a boolean" % value)
            self.p.npy_bool_ptr.set_one(idx, ival)
        else:
            raise TypeError("__setitem__: dtype = %s not understood." % self.dtype)

    ###### Arithmetics #######

    # XXX maybe add more arithm ops: div, l/r shifts, mod etc

    {{for op, symb in zip(["add", "mul", "sub"], ["+", "*", "-"])}}
    def __i{{op}}__(MapArray self not None, other):
        if isinstance(other, np.ndarray): 
            # hand over to __{{op}}__ for densification
            return NotImplemented

        if isinstance(other, MapArray):
            return _i{{op}}_impl(self, other)

        # else, other is a scalar.
        dt = np.asarray(other).dtype     # XXX there must be a more direct way
        arr_other = MapArray(shape=self.shape,
                             dtype=dt,
                             fill_value=other)
        return _i{{op}}_impl(self, arr_other)

    def __{{op}}__(self, other):
        if isinstance(self, MapArray):
            if isinstance(other, np.ndarray):
                # Densify and return dense result
                return self.todense() {{symb}} other
            else:
                newobj = self.copy()
                return newobj.__i{{op}}__(other)
        else:
            # other must be a MapArray
            if isinstance(self, np.ndarray):
                return self {{symb}} other.todense()

            # else, self must be a scalar
            dt = np.asarray(self).dtype     # XXX there must be a more direct way
            arr_self = MapArray(shape=other.shape,
                                 dtype=dt,
                                 fill_value=self)
            return _i{{op}}_impl(arr_self, other)
    {{endfor}}

    ########### Booleans ########################

    def __richcmp__(self, other, int op):
        if not isinstance(self, MapArray):
            # __richcmp__ seems to sort out reversed ops ($1 < m$ -> $m >= 1$)
            # automagically
            raise ValueError("__richcmp__: %s %s %s" % (self, other, op))

        if isinstance(other, np.ndarray):
            return PyObject_RichCompare(self.todense(), other, op)

        if isinstance(other, MapArray):
            return _richcmp_impl(self, other, op)

        # else, other is a scalar
        dt = np.asarray(other).dtype     # XXX there must be a more direct way
        arr_other = MapArray(shape=self.shape,
                             dtype=dt,
                             fill_value=other)
        return _richcmp_impl(self, arr_other, op)

    def __matmul__(self, A):
        if isinstance(self, np.ndarray):
            return self.__matmul__(A.todense())
        if isinstance(A, np.ndarray):
            return self.todense().__matmul__(A)

        if not isinstance(self, MapArray) or not isinstance(A, MapArray):
            raise NotImplementedError("matmul only handles sparse arrays.")

        if self.fill_value != 0 or A.fill_value != 0:
            raise NotImplementedError("The result is likely dense.")

        # figure out the result dtype and perform multiplication
        dtyp = np.promote_types(self.dtype, A.dtype)
        newobj = MapArray(dtype=dtyp)
        _mmul_impl(newobj, self, A)
        return newobj

    def __imatmul__(self, A):
        raise NotImplementedError("In-place matmul is not implemented. "
                                  "Use a = a @ b instead of a @= b.")

##### implementations

def _mmul_impl(MapArray self not None,
               MapArray A not None,
               MapArray B not None):

    # handle types
    if A.dtype != self.dtype:
        AA = A.astype(self.dtype)
        return _mmul_impl(self, AA, B)
    if B.dtype != self.dtype:
        BB = B.astype(self.dtype)
        return _mmul_impl(self, A, BB)

    # by now all arrays are of the same dtype
    {{for NUM, CT in zip(TNUMS, CTYPES)}}
    if self.typenum == {{NUM}}:
        op_{{CT}}.apply_mmul(self.p.{{CT}}_ptr,
                             A.p.{{CT}}_ptr,
                             B.p.{{CT}}_ptr)
        return self
    {{endfor}}
    raise NotImplementedError


def _richcmp_impl(MapArray self not None, MapArray other not None, int op):
    """ Sparse <op> sparse implementation of __richcmp__.

        Figure out the common dtype, copy if necessary. Then dispatch
        on the type.

        XXX: this can be made copy less (ditto for binops).
    """
    # figure out dtypes, copy if necessary         
    if self.typenum != other.typenum:
        res_type = np.promote_types(self.dtype, other.dtype)
        if res_type.num != self.typenum:
            # upcast self and operate on the copy
            newobj = self.astype(res_type)
            return _richcmp_impl(newobj, other, op)

        # by this stage, self.dtype == res_type
        if self.typenum != other.typenum:
            # upcast other
            other = other.astype(res_type)

    # by now, both self and other are of the same dtype
    result = MapArray(dtype=bool)

    # dispatch on the types
    {{for NUM, CT in zip(TNUMS, CTYPES)}}
    if self.typenum == {{NUM}}:
        if op == 0:
            op_npy_bool.apply_binop[{{CT}}](less[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 1:
            op_npy_bool.apply_binop[{{CT}}](less_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 2:
            op_npy_bool.apply_binop[{{CT}}](equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 3:
            op_npy_bool.apply_binop[{{CT}}](not_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 4:
            op_npy_bool.apply_binop[{{CT}}](greater[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        elif op == 5:
            op_npy_bool.apply_binop[{{CT}}](greater_equal[{{CT}}],
                                          result.p.npy_bool_ptr,
                                          self.p.{{CT}}_ptr,
                                          other.p.{{CT}}_ptr)
        else:
            raise ValueError("sparse cmp: op = ." % op)
        return result

    {{endfor}}


{{for op in ["add", "mul", "sub"]}}
def _i{{op}}_impl(MapArray self not None, MapArray other not None):
    """ Sparse <op> sparse implementation of __i{{op}}__.

        Figure out the common dtype, copy if necessary. Then dispatch
        on the type.
    """
    # figure out dtypes, copy if necessary
    if self.typenum != other.typenum:
        res_type = np.promote_types(self.dtype, other.dtype)
        if res_type.num != self.typenum:
            # upcast self and operate on the copy
            newobj = self.astype(res_type)
            return _i{{op}}_impl(newobj, other)

        # by this stage, self.dtype == res_type
        if self.typenum != other.typenum:
            # upcast other
            other = other.astype(res_type)

    # dispatch
    {{for num, ct in zip(TNUMS, CTYPES)}}
    if self.typenum == {{num}}:
        op_{{ct}}.inplace_binop({{op}}[{{ct}}],
                                self.p.{{ct}}_ptr,
                                other.p.{{ct}}_ptr)
        return self
    {{endfor}}
    raise NotImplementedError("_i{{op}}_: typecode %s not understood." % self.typenum)
{{endfor}}


cnp.import_array()
